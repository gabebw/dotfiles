#!/bin/zsh

### ~*~ t ~*~ ###
# Use `t blog` to connect to the `blog` session, or create a new session named
# `blog`.
#
# With no arguments, fuzzy-finds a session in a directory using the `projects`
# script.

if [[ "$1" == "with-tmux" ]]; then
  USE_FZF_TMUX=true
  shift
else
  USE_FZF_TMUX=false
fi

_not_in_tmux(){
  [[ -z "$TMUX" ]]
}

# Attach if not in tmux, or switch if we are in tmux
_attach_to_tmux_session() {
  session=$1

  if _not_in_tmux; then
    tmux attach -d -t "$session"
  else
    tmux switch-client -t "$session"
  fi
}

# tmux doesn't allow . in session names
_tmux_normalize_session_name(){
  basename "${1//./-}"
}

# Create a new tmux session with the given name.
# It's created in the background (`-d`) so it can be created when already in
# another tmux session.
_new_tmux_session_named() {
  local new_session_name=$1
  local dir=$2

  # -A means "attach if this already exists, otherwise create it"
  if [ -n "$dir" ]; then
    tmux new-session -d -As "$new_session_name" -c "$dir"
  else
    tmux new-session -d -As "$new_session_name"
  fi
  _attach_to_tmux_session "$new_session_name"
}

# Try to connect to a tmux session with the given name.
# If no such session exists, create it first, optionally inside a given
# directory.
_tmux_try_to_connect_to(){
  local name=$1
  local dir=$2
  if tmux-session-exists "$name"; then
    _attach_to_tmux_session "$name"
  else
    _new_tmux_session_named "$name" "$dir"
  fi
}

# Are we currently connected to the given session?
_connected_to(){
  current_tmux_session=$(tmux ls -F '#S #{session_attached}' 2>/dev/null | grep '1$' | cut -d' ' -f1)

  [[ "$1" == "$current_tmux_session" ]]
}

if (( $# == 1 )); then
  _tmux_try_to_connect_to "$(_tmux_normalize_session_name "$1")"
elif (( $# == 2 )); then
  _tmux_try_to_connect_to "$(_tmux_normalize_session_name "$1")" "$2"
else
  if $USE_FZF_TMUX; then
    project_directory=$(projects | fzf-tmux --reverse -d '85%')
  else
    project_directory=$(projects | fzf --reverse)
  fi

  if [[ -n "$project_directory" ]]; then
    session_name=$(_tmux_normalize_session_name "$project_directory")

    if ! _connected_to "$session_name"; then
      if tmux-session-exists "$session_name"; then
        _attach_to_tmux_session "$session_name"
      else
        (cd "$project_directory" && _new_tmux_session_named "$session_name")
      fi
    fi
  fi
fi
