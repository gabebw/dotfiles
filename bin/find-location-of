#!/usr/bin/env zsh

# Finds a script file, shell alias, or shell function and prints its
# location, including a the line number. The line number is always included for
# consistency, even if it's just "1" (i.e. start at the top, just like the
# default).
#
# Usage:
#
#     $ find-location-of myalias
#     ~/.zshrc:530
#
#     $ find-location-of git-merge-to
#     ~/.bin/git-merge-to:1
#
#     $ find-location-of which
#     which is a shell builtin (printed to STDERR)
#     exits 1

source ~/.zshrc

_handle_unknown_command() {
  echo "\"$1\" is not recognized as a script, function, or alias" >&2
  echo "(searched in $(_search_paths))" >&2
  exit 1
}

_follow_symlink(){
  if [[ -L "$1" ]]; then
    readlink "$1"
  else
    print "$1"
  fi
}

_definition_type() {
  arg_type_string=$(type -a "$1")
  if [[ "$arg_type_string" == *"is an alias"* ]]; then
    echo "alias"
  elif [[ "$arg_type_string" == *"is a shell function"* ]]; then
    echo "function"
  elif [[ "$arg_type_string" == *"is a shell builtin"* ]]; then
    echo "builtin"
  elif [[ "$arg_type_string" == *"reserved word"* ]]; then
    echo "builtin"
  elif [[ "$arg_type_string" == *"is an autoload shell function"* ]]; then
    # Can't edit these, but they do show up in `which`, so short-circuit here
    echo "not found"
  elif which "$1" > /dev/null 2>&1; then
    echo "script"
  else
    echo "not found"
  fi
}

_edit_script() {
  local script_path=$(_follow_symlink "$(which "$1")")
  echo "$script_path":1
}

_definition_field_for_pattern() {
  _grep_for_definition -n "$2" | cut -d ":" -f "$1"
}

_search_paths(){
  echo ~/.zshrc ~/.zshenv ~/.aliases
}

_grep_for_definition(){
  grep -E "$@" $(_search_paths) 2>/dev/null
}

_edit_based_on_pattern(){
  local pattern=$1
  local command_name=$2

  if _grep_for_definition -q "$pattern"; then
    file=$(_definition_field_for_pattern 1 "$pattern")
    line=$(_definition_field_for_pattern 2 "$pattern")
    echo "$(_follow_symlink "$file")":"$line"
  else
    _handle_unknown_command "$command_name"
  fi
}

_edit_shell_function() {
  _edit_based_on_pattern "(^function $1[ {()])|(^$1\(\))" "$1"
}

_edit_shell_alias() {
  _edit_based_on_pattern "alias $1=" "$1"
}

if [[ $# == 0 ]]; then
  echo "No argument given" >&2
  exit 1
else
  command_name="$1"
  case "$(_definition_type "$command_name")" in
    "alias") _edit_shell_alias "$command_name";;
    "function") _edit_shell_function "$command_name";;
    "builtin")
      echo "$command_name is a shell builtin" >&2
      exit 1
    ;;
    "script") _edit_script "$command_name";;
    "not found") _handle_unknown_command "$command_name"
  esac
fi
