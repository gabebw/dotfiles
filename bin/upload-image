#!/usr/bin/env fish

# Safely (no overwriting) upload a local file to i.gabebw.com
#
# Upload ./flip.jpg as flip.jpg:
#
#   upload-image flip.jpg
#
# Upload ./flip.jpg as other.jpg:
#
#   upload-image flip.jpg other.jpg
#
# Upload ./flip.jpg as some/other.jpg and create the `some/` directory if it
# doesn't exist:
#
#   upload-image flip.jpg some/other.jpg

set rsync_major_version_number (rsync --version | rg 'version' | sed -E 's/^rsync  version ([0-9]).*/\1/')

if [ $rsync_major_version_number -le 2 ]
  echo "You are using Rsync v2, which does not support --secluded-args" >&2
  echo "Upgrade: brew install rsync" >&2
  exit 1
end

set IMAGES_DIR /nginx-data/images

function create_missing_directories_on_remote_host -a filename
  if [ $filename != (basename $filename) ]
    ssh i "cd '$IMAGES_DIR' && mkdir -p '$(dirname "$filename")'"
  end
end

set local_filename $argv[1]
if [ (count $argv) -lt 2 ]
  set filename_on_server $argv[1]
else
  set filename_on_server $argv[2]
end

create_missing_directories_on_remote_host $filename_on_server

rsync -e ssh -azh --ignore-existing --secluded-args --chmod=ug=rw,o=r $local_filename i:$IMAGES_DIR/$filename_on_server
open http://i.gabebw.com/$filename_on_server
